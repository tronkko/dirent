cmake_minimum_required(VERSION 3.30)
project(dirent C CXX)

add_library(dirent INTERFACE)

if(NOT CMAKE_C_COMPILER_ID STREQUAL "MSVC")
  return()
endif()

target_include_directories(dirent INTERFACE include)

include(CTest)

option(DIRENT_BUILD_EXAMPLES "" ON)

if(${DIRENT_BUILD_EXAMPLES})
  block()
    file(GLOB examples examples/*)
    foreach(path IN LISTS examples)
      get_filename_component(name ${path} NAME_WE)
      set(target ${PROJECT_NAME}_example_${name})
      add_executable(${target} ${path})
      target_link_libraries(${target} dirent)
    endforeach()
  endblock()
endif()

if(${BUILD_TESTING})
  block()
    file(GLOB glob LIST_DIRECTORIES TRUE tests/*)
    foreach(path IN LISTS glob)
      if(IS_DIRECTORY ${path})
        file(COPY ${path} DESTINATION ${CMAKE_CURRENT_BINARY_DIR}/tests)
      else()
        get_filename_component(name ${path} NAME_WE)
        string(SUBSTRING ${name} 2 -1 name)
        set(target ${PROJECT_NAME}_test_${name})
        add_executable(${target} ${path})
        target_link_libraries(${target} PRIVATE dirent)
        add_test(NAME ${target} COMMAND ${target})
        set_tests_properties(${target} PROPERTIES SKIP_RETURN_CODE 77)
      endif()
    endforeach()
  endblock()
endif()